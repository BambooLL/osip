dnl Process this file with autoconf to produce a configure script.
AC_INIT(osip/osip.h)



dnl *********************************************************************
dnl Source packaging numbers
OSIP_MAJOR_VERSION=0
OSIP_MINOR_VERSION=8
OSIP_MICRO_VERSION=1

dnl Library extension
OSIP_RELEASE=0.8.1

OSIP_VERSION=$OSIP_MAJOR_VERSION.$OSIP_MINOR_VERSION.$OSIP_MICRO_VERSION

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work 
#                 better
# CURRENT : REVISION : AGE
LIBOSIP_SO_VERSION=0:0:0

AC_SUBST(LIBOSIP_SO_VERSION, $LIBOSIP_SO_VERSION)

AC_SUBST(OSIP_RELEASE, $OSIP_RELEASE)
AC_SUBST(OSIP_VERSION)

VERSION=$OSIP_VERSION
PACKAGE=libosip


dnl *********************************************************************
dnl Initialize automake stuff
AC_CONFIG_AUX_DIR(scripts)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AC_SUBST(ac_aux_dir)

dnl Checks for programs.

AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL


dnl Initialize libtool
AC_PROG_LIBTOOL
AM_PROG_LIBTOOL
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)

dnl declare --enable-* args and collect ac_help strings

dnl build with multithreaded support (need semaphore).
AC_ARG_ENABLE(mt,
[  --disable-mt              compile oSIP without multi-thread support.],
enable_mt=$enableval,enable_mt="yes")

dnl md5 support.
AC_ARG_ENABLE(md5,
[  --disable-md5             compile oSIP without MD5 support.],
enable_md5=$enableval,enable_md5="yes")

dnl buffer save mode support.
AC_ARG_ENABLE(buffermode,
[  --disable-buffermode      compile oSIP without buffer save mode support.],
enable_buffermode=$enableval,enable_buffermode="yes")

dnl support for linux-thread or posix thread (pthread.h)
AC_ARG_ENABLE(pthread,
[  --disable-pthread         disable support for POSIX threads.],
enable_pthread=$enableval,enable_pthread="yes")

dnl support for GNU Portable Threads
AC_ARG_ENABLE(pth,
[  --enable-pth              enable support for GNU portable thread.],
enable_pth=$enableval,enable_pth="no")

AC_ARG_ENABLE(debug,
[  --disable-debug           turn off debugging.],
disable_debug=$enableval,disable_debug="yes")

AC_ARG_ENABLE(trace,
[  --disable-trace           turn off trace.],
disable_trace=$enableval,disable_trace="yes")

AC_ARG_ENABLE(mleak,
[  --enable-mleak            turn on memory leak detection.],
enable_mleak=$enableval,enable_mleak="no")


dnl compile with mt support
if test "x$enable_mt" = "xyes"; then

  SIP_FLAGS="$SIP_FLAGS -DOSIP_MT"
  if test "x$enable_pth" = "xyes"; then
    SIP_FSM_FLAGS="$SIP_FSM_FLAGS `pth-config --cflags` -DTHREAD_PTH"
    FSM_LIB="$FSM_LIB `pth-config --ldflags` `pth-config --libs`"
  else
    if test "x$enable_pthread" = "xyes"; then
      SIP_FSM_FLAGS="$SIP_FSM_FLAGS -DTHREAD_PTHREAD"
      FSM_LIB="$FSM_LIB -lpthread"
    fi
  fi
fi

SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -pedantic"

if test "x$enable_md5" = "xno"; then
  SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -DNO_MD5_SUPPORT"
fi

if test "x$enable_buffermode" = "xyes"; then
  SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -DUSE_TMP_BUFFER"
fi

if test "x$disable_debug" = "xyes"; then
  SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -DENABLE_DEBUG -g"
fi

if test "x$disable_trace" = "xyes"; then
  SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -DENABLE_TRACE"
fi

if test "x$enable_mleak" = "xyes"; then
  SIP_EXTRA_FLAGS="$SIP_EXTRA_FLAGS -DENABLE_LEAKD"
fi


dnl Checks for libraries.
AC_CHECK_LIB(posix4,sem_open,[FSM_LIB="$FSM_LIB -lposix4 -mt"])
AC_CHECK_LIB(nsl,nis_add,[FSM_LIB="$FSM_LIB -lnsl"])
AC_CHECK_LIB(socket,sendto,[FSM_LIB="$FSM_LIB -lsocket"])

AC_SUBST(SIP_CFLAGS)
AC_SUBST(SIP_FLAGS)

AC_SUBST(SIP_EXTRA_FLAGS)
AC_SUBST(SIP_PARSER_FLAGS)
AC_SUBST(SIP_FSM_FLAGS)

AC_SUBST(EXTRA_LIB)
AC_SUBST(PARSER_LIB)
AC_SUBST(FSM_LIB)

AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(ctype.h)
AC_CHECK_HEADERS(pthread.h)
AC_CHECK_HEADERS(string.h)
AC_CHECK_HEADERS(strings.h)
AC_CHECK_HEADERS(stdio.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(stdarg.h)
AC_CHECK_HEADERS(varargs.h)
AC_CHECK_HEADERS(fcntl.h sys/time.h assert.h signal.h sys/signal.h malloc.h) 


AC_OUTPUT(Makefile parser/Makefile fsm/Makefile test/Makefile osip/Makefile scripts/Makefile doc/Makefile conf/Makefile windows/Makefile)

